group 'eu.pretix.pretixdesk'
version = '0.3.3'
version version

buildscript {
    ext.kotlin_version = '1.3.20'

    repositories {
        jcenter()
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'edu.sc.seis.gradle:launch4j:2.4.2'
        classpath "net.ltgt.gradle:gradle-apt-plugin:0.17"
        classpath group: 'de.dynamicfiles.projects.gradle.plugins', name: 'javafx-gradle-plugin', version: '8.8.2'
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'application'
apply plugin: 'javafx-gradle-plugin'
apply plugin: 'edu.sc.seis.launch4j'
apply plugin: 'kotlin-kapt'
apply plugin: 'net.ltgt.apt-idea'

sourceCompatibility = 1.8
mainClassName = 'eu.pretix.pretixdesk.PretixDeskMain'

repositories {
    mavenCentral()
}

compileKotlin {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    kotlinOptions {
        jvmTarget = "1.8"
        apiVersion = "1.3"
        languageVersion = "1.3"
    }
}
dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'no.tornado:tornadofx:1.7.14'
    implementation 'org.xerial:sqlite-jdbc:3.20.1'
    implementation 'net.harawata:appdirs:1.0.0'
    implementation 'net.java.dev.jna:platform:3.5.2'
    implementation 'joda-time:joda-time:2.9.9'
    implementation "de.huxhorn.lilith:de.huxhorn.lilith.3rdparty.junique:1.0.4"
    implementation 'com.github.kittinunf.fuel:fuel:1.12.0'
    implementation 'de.jensd:fontawesomefx-materialicons:2.2.0-5'
    implementation 'com.jfoenix:jfoenix:8.0.3'

    // libpretixsync
    implementation 'com.squareup.okhttp3:okhttp:3.10.0'
    implementation 'io.requery:requery:1.5.1'
    implementation 'io.requery:requery-kotlin:1.5.1'
    implementation 'joda-time:joda-time:2.9.9'
    implementation 'org.json:json:20090211'
    annotationProcessor 'javax.annotation:jsr250-api:1.0'
    annotationProcessor 'io.requery:requery-processor:1.5.1'

    implementation(project(':libpretixsync')) {
        transitive = false
    }
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

jfx {
    mainClass = mainClassName
    vendor = 'pretix.eu'
    jfxMainAppJarName = "pretixdesk-${version}.jar"

    // gradle jfxNative
    identifier = null
    nativeReleaseVersion = version
    needShortcut = false
    needMenu = false
    appName = "pretixdesk"
}

task fatJar(type: Jar) {
    manifest {
        attributes 'Implementation-Version': version,
                'Main-Class': mainClassName
    }
    archiveName = project.name + '.jar'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

launch4j {
    mainClassName = mainClassName
    jar = "lib/${project.tasks.fatJar.archiveName}"
    copyConfigurable = project.tasks.fatJar.outputs.files
    bundledJrePath = "jre"
    bundledJre64Bit = true
    version = version
    fileDescription = "pretix Check-in application"
    copyright = "pretix.eu"
    companyName = "pretix.eu"
    productName = "pretixdesk"
    //splashFileName = "../../../img/splash.bmp"
    icon = "../../../img/icon_on_shape.ico"
}
